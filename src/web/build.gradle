import com.wiredforcode.gradle.spawn.*

buildscript {
    repositories {
      maven { url 'https://plugins.gradle.org/m2/' }
      maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
    }
    dependencies {
      classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
      classpath 'com.wiredforcode:gradle-spawn-plugin:0.8.0'
    }
}

defaultTasks 'ngServe'

apply plugin: 'com.moowork.node'
apply plugin: 'com.wiredforcode.spawn'

group 'at.irro.dipl'
version '1.0-SNAPSHOT'
description = ''

node {
    version = '9.3.0'
    npmVersion = '5.6.0'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = false

    nodeModulesDir = file("./node_modules")
}

task startNgServe(type: SpawnProcessTask, dependsOn: 'npmInstall') {
    command "node_modules/@angular/cli/bin/ng serve"
    ready 'webpack: bundle is now VALID.'
}

task stopNgServe(type: KillProcessTask)

task ngLint(type: Exec, dependsOn: 'npmInstall') {
    commandLine 'node_modules/@angular/cli/bin/ng', 'lint'
}

task ngTest(type: Exec, dependsOn: 'npmInstall') {
    commandLine 'node_modules/@angular/cli/bin/ng', 'test', '--watch=false'
}

task ngServe(type: Exec, dependsOn: 'npmInstall') {
    commandLine 'node_modules/@angular/cli/bin/ng', 'serve', '--proxy-config=proxy.config.json'
}

task ngE2E(type: Exec, dependsOn: ['npmInstall', 'startNgServe']) {
    commandLine 'node_modules/@angular/cli/bin/ng', 'e2e'
}

/* these should go too...but they dont..
ngE2E.finalizedBy stopNgServe

test.dependsOn('ngTest')

check.dependsOn('ngLint')
*/

// Generating gradle wrapper so people don't have to have the Gradle version preinstalled
task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
  distributionType = Wrapper.DistributionType.ALL
}

